import run.GUI;
import org.compsys704.*;


ConveyorPlant(
		input signal ConveyorON, ConveyorOFF, emergencyStopPlant;
		output signal ConveyorONe, ConveyorOFFe;
		)
->
{

	System.out.println("ConveyorPlant");
	{	
		while(true) {
			await(ConveyorON && !emergencyStopPlant);
			{
				System.out.println("Conveyor ON");
				emit ConveyorONe;
				
			}
			await(ConveyorOFF || emergencyStopPlant);
			{
				System.out.println("Conveyor OFF");
				emit ConveyorOFFe;
				
			}
		}
	}
}

RotaryPlant(
		input signal turn, stop;
		output signal turnE, stopE;
		
		)
->
{

	System.out.println("RotaryPlant");
	{	
		while(true) {
			await(turn);
			{
				System.out.println("TURN");
				
			}
			await(stop);
			{
				System.out.println("STOP");
				
			}

		}
		
	}
	
	
	||
	{// To viz
		{while(true){present(turn){emit turnE;}; pause;}}
		||
		{while(true){present(stop){emit stopE;}; pause;}}
	}
}

CapLoaderPlant(
		input signal pusherExtend,vacOn,armSource,armDest;
		input signal enable,refill;
		input Integer signal modeGUI;
		output signal pusherRetracted,pusherExtended,WPgripped,armAtSource,armAtDest,empty;
		output signal pusherRetractedE,pusherExtendedE,WPgrippedE,armAtSourceE,armAtDestE,emptyE;
		output Integer signal mode;
		)
->
{
	System.out.println("CapLoaderPlant");

	signal capDec;
	int signal capPos;
	emit capPos(0);
	{// Arm
		while(true){
			abort(armSource && enable){
				sustain armAtDest;
			}
			await(!enable);
			abort(armDest && enable){
				sustain armAtSource;
			}
			await(!enable);
		}
	}
	||
	{// Pusher
		while(true){
			abort(pusherExtend && enable){
				sustain pusherRetracted;
			}
			await(!enable);
			abort(!pusherExtend && enable){
				sustain pusherExtended;
			}
			await(!enable);
		}
	}
	||
	{// Vacuum
		while(true){
			present(vacOn && armAtSource){
				if((Integer)#capPos == 1){
					emit capPos(0);
					abort(!vacOn){
						sustain WPgripped; 
					}
					present(armAtSource){
						emit capPos(1);
					}
				}
			}
			pause;
		}
	}
	||
	{
		/*
		 * Cap positions
		 * 0 = at Mag
		 * 1 = at pick-up
		 */
		while(true){
			// At mag
			abort(empty){
				while(true){
						await(pusherRetracted);
						await(pusherExtended);
						// At pick-up
						emit capPos(1);
						await(pusherRetracted);
						emit capDec;
				}
			}
			await(refill);
		}
	}
	||
	{
		int capcount = 5;
		while(true){
			present(capDec){
				if(capcount > 0)
					capcount--;
			}
			present(refill){
				capcount = 5;
			}
//			present(pusherRetracted){
				if(capcount == 0)
					emit empty;
//			}
			pause;
		}
	}
	||
	{// To viz
		{while(true){present(pusherRetracted){emit pusherRetractedE;} pause;}}
		||
		{while(true){present(pusherExtended){emit pusherExtendedE;} pause;}}
		||
		{while(true){present(WPgripped){emit WPgrippedE;} pause;}}
		||
		{while(true){present(armAtSource){emit armAtSourceE;} pause;}}
		||
		{while(true){present(armAtDest){emit armAtDestE;} pause;}}
		||
		{while(true){present(empty){emit emptyE;} pause;}}
	}
	||
	{// Mode selection
		{while(true){
			present(modeGUI){
				mode = modeGUI;
				emit mode;
			}
			pause;
		}
	}
}
}

CapperPlant(
		input Integer signal modeGUI;
		output Integer signal mode;

		input signal clampBottle,gripCap,lowerCapper,turnGripperFinal,turnGripperHome;
		input signal enable,bottlePos5;
		output signal clampClose,clampOpen,gripperClose,gripperOpen,capperRaised,capperLowered,gripperAtFinal,gripperAtHome,bottlePos5Disp;
		output signal clampCloseE,clampOpenE,gripperCloseE,gripperOpenE,capperRaisedE,capperLoweredE,gripperAtFinalE,gripperAtHomeE,bottlePos5DispE;
		)
->
{
	System.out.println("CapperPlant");
	{// Bottle Clamp
		while(true) {
			abort(clampBottle && enable){
				sustain clampOpen;
			}
			await(!enable);
			abort(!clampBottle && enable){
				sustain clampClose;
			}
			await(!enable);
		}
	}
	||
	{// Gripper Pneumatic
		while(true) {
			abort(gripCap && enable){
				sustain gripperOpen;
			}
			await(!enable);
			abort(!gripCap && enable){
				sustain gripperClose;
			}
			await(!enable);
		}
	}
	||
	{// Rotation Pneumatic
		while(true) {
			abort(turnGripperFinal && enable){
				sustain gripperAtHome;
			}
			await(!enable);
			abort(turnGripperHome && enable){
				sustain gripperAtFinal;
			}
			await(!enable);
		}
	}
	||
	{// Linear Pneumatic
		while(true) {
			abort(lowerCapper && enable){
				sustain capperRaised;
			}
			await(!enable);
			abort(!lowerCapper && enable){
				sustain capperLowered;
			}
			await(!enable);
		}
	}
	||
	{// Display bottle to GUI
		while(true) {
			abort(bottlePos5 && capperRaised){
				halt;
			}
			pause;
			abort(!bottlePos5){
				sustain bottlePos5Disp;
			}
		}
	}
	||
	{// To viz
		{while(true){present(clampClose){emit clampCloseE;} pause;}}
		||
		{while(true){present(clampOpen){emit clampOpenE;} pause;}}
		||
		{while(true){present(gripperClose){emit gripperCloseE;} pause;}}
		||
		{while(true){present(gripperAtHome){emit gripperAtHomeE;} pause;}}
		||
		{while(true){present(capperRaised){emit capperRaisedE;} pause;}}
		||
		{while(true){present(capperLowered){emit capperLoweredE;} pause;}}
		||
		{while(true){present(gripperAtFinal){emit gripperAtFinalE;} pause;}}
		||
		{while(true){present(bottlePos5Disp){emit bottlePos5DispE;} pause;}}
	}
	||
	{// Mode selection
		{while(true){
			present(modeGUI){
				mode = modeGUI;
				emit mode;
			}
			pause;
		}
	}
}
}

BaxterPlant(
		input String signal CMDR,CMDL;
		input signal enable;

		output signal CMDRfb, CMDLfb;
		output signal LAE, LBE, LCE, LDE, LOE, LGE;
		output signal RAE, RBE, RCE, RDE, ROE, RGE;
		)
->
{
	System.out.println("BaxterPlant");
//	new Thread(new GUI()).start();
	{//Right Arm
		loop{
			present(CMDR && enable){
				await(!enable);
				if(#CMDR.contains("right_limb")) {
					if(#CMDR.contains("limb_moveto")) {
						if(#CMDR.contains("A")) {
							emit RAE;
						} else if (#CMDR.contains("B")) {
							emit RBE;
						} else if (#CMDR.contains("C")) {
							emit RCE;
						} else if (#CMDR.contains("D")) {
							emit RDE;
						} else {
							System.err.println("Incorrect position argument");
						}
					} else if(#CMDR.contains("limb_gripper")) {
						if(#CMDR.contains("open")) {
							emit ROE;
						} else if (#CMDR.contains("close")) {
							emit RGE;
						} else {
							System.err.println("Incorrect position argument");
						}
					}
					emit CMDRfb;
				}
			}
			pause;
		}
	}
	||
	{//Right Arm
		loop{
			present(CMDL && enable){
				await(!enable);
				if(#CMDL.contains("left_limb")) {
					if(#CMDL.contains("limb_moveto")) {
						if(#CMDL.contains("A")) {
							emit LAE;
						} else if (#CMDL.contains("B")) {
							emit LBE;
						} else if (#CMDL.contains("C")) {
							emit LCE;
						} else if (#CMDL.contains("D")) {
							emit LDE;
						} else {
							System.err.println("Incorrect position argument");
						}
					} else if(#CMDL.contains("limb_gripper")) {
						if(#CMDL.contains("open")) {
							emit LOE;
						} else if (#CMDL.contains("close")) {
							emit LGE;
						} else {
							System.err.println("Incorrect position argument");
						}
					}
					emit CMDLfb;
				}
			}
			pause;
		}
	}
	||
	{
		{loop{present(LAE){System.out.println("LA");} pause;}}
		||
		{loop{present(LBE){System.out.println("LB");} pause;}}
		||
		{loop{present(LCE){System.out.println("LC");} pause;}}
		||
		{loop{present(LDE){System.out.println("LD");} pause;}}
		||
		{loop{present(LOE){System.out.println("LO");} pause;}}
		||
		{loop{present(LGE){System.out.println("LG");} pause;}}
		||
		{loop{present(RAE){System.out.println("RA");} pause;}}
		||
		{loop{present(RBE){System.out.println("RB");} pause;}}
		||
		{loop{present(RCE){System.out.println("RC");} pause;}}
		||
		{loop{present(RDE){System.out.println("RD");} pause;}}
		||
		{loop{present(ROE){System.out.println("RO");} pause;}}
		||
		{loop{present(RGE){System.out.println("RG");} pause;}}
	}
}

ECSPlant(
		input List signal tempState,humidityState,lightState,presenceState;
		input int signal smokeState,closedState;
		output signal updatep,updates;
		
		output signal tempHotE,tempColdE,tempOffE,humidityIncE,humidityRedE,humidityOffE,lightOffE,emergency;
		output int signal lightOnE;
		)
->
{
	new Thread(new GUI()).start();
	{
		System.out.println("ECSPlant");
		loop {// Environment Reaction
			// Synchronously receive all information from controller
			await(tempState);
			abort(!tempState){
				sustain updatep;
			}
			await(humidityState);
			abort(!humidityState){
				sustain updatep;
			}
			await(lightState);
			abort(!lightState){
				sustain updatep;
			}
			await(presenceState);
			abort(!presenceState){
				sustain updatep;
			}
			await(closedState);
			abort(!closedState){
				sustain updatep;
			}
			
			// For loop sets state of each environment actuator in the plant
			List tempOut 		= #tempState;
			List humidityOut 	= #humidityState;
			List lightOut 		= #lightState;
			List presence 		= #presenceState;
			int closed 			= #closedState;
			for(int i = 0; i < 7; i++) {
				States.CURR_ZONE = i;
				if (((int)tempOut.get(i) == 0) && (((Boolean)presence.get(i) == true) || ((int)closed == 0))) {
					emit tempHotE;
					await(!tempHotE);
				} else if (((int)tempOut.get(i) == 2) && (((Boolean)presence.get(i) == true) || ((int)closed == 0))) {
					emit tempColdE;
					await(!tempColdE);
				} else {
					emit tempOffE;
					await(!tempOffE);
				}
				if (((int)humidityOut.get(i) == 0) && (((Boolean)presence.get(i) == true) || ((int)closed == 0))) {
					emit humidityIncE;
					await(!humidityIncE);
				} else if (((int)humidityOut.get(i) == 2) && (((Boolean)presence.get(i) == true) || ((int)closed == 0))) {
					emit humidityRedE;
					await(!humidityRedE);
				} else {
					emit humidityOffE;
					await(!humidityOffE);
				}
				if (((int)lightOut.get(i) > 0) && (((Boolean)presence.get(i) == true) || ((int)closed == 0))) {
					emit lightOnE(lightOut.get(i));
					await(!lightOnE);
				} else {
					emit lightOffE;
					await(!lightOffE);
				}
				pause;
			}
		}
	}
	||
	{// Smoke/Fire Reaction
		// Smoke/Fire detection reaction separated from other environment conditions since emergency signals should be sent ASAP
		loop{
			// Synchronously receive smoke information from controller
			await(smokeState);
			abort(!smokeState) {
				sustain updates;
			}
			int smoke = #smokeState;
			if ((int)smoke == 1) {
				States.SMOKE_STATE = 1;
			} else {
				States.SMOKE_STATE = 0;
			}
		}
	}
}

SACSPlant(
		input signal open_door1, open_door2, open_door3, close_door1, close_door2, close_door3, hp1, hp2, hp3, hp4, hp5, hp6, hp7;
		output signal door1_opened, door2_opened, door3_opened, door1_closed, door2_closed, door3_closed;
		
		)
->
{
//	new Thread(new GUI()).start();

	{	
		while(true) {
			
			loop{
				await(open_door1 || open_door2 || open_door3);
				{
					System.out.println("Open Door");
					
				}
			}
			||
			
			loop{
				await(close_door1 || close_door2 || close_door3);
				{
					System.out.println("Close Door");
					
				}
			}
			
			||
			
			loop{
				await(hp1);
				{
					System.out.println("Human Detected in zone 1");
				}
			}
			||
			
			loop{
				await(hp2);
				{
					System.out.println("Human Detected in zone 2");
				}
			}
			||
			
			loop{
				await(hp3);
				{
					System.out.println("Human Detected in zone 3");
				}
			}
			||
			
			loop{
				await(hp4);
				{
					System.out.println("Human Detected in zone 4");
				}
			}
			||
			
			loop{
				await(hp5);
				{
					System.out.println("Human Detected in zone 5");
				}
			}
			||
			
			loop{
				await(hp6);
				{
					System.out.println("Human Detected in zone 6");
				}
			}
			||
			
			loop{
				await(hp7);
				{
					System.out.println("Human Detected in zone 7");
				}
			}

				
		}
		
	}
	
	
	||
	{// To viz
		{while(true){present(open_door1){emit door1_opened;}; pause;}}
		||
		{while(true){present(open_door2){emit door2_opened;}; pause;}}
		||
		{while(true){present(open_door3){emit door3_opened;}; pause;}}
		||
		{while(true){present(close_door1){emit door1_closed;} pause;}}
		||
		{while(true){present(close_door2){emit door2_closed;} pause;}}
		||
		{while(true){present(close_door3){emit door3_closed;} pause;}}
	}
}